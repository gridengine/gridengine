#!/bin/csh -fb
#  aimk
#
#___INFO__MARK_BEGIN__
##########################################################################
#
#  The Contents of this file are made available subject to the terms of
#  the Sun Industry Standards Source License Version 1.2
#
#  Sun Microsystems Inc., March, 2001
#
#
#  Sun Industry Standards Source License Version 1.2
#  =================================================
#  The contents of this file are subject to the Sun Industry Standards
#  Source License Version 1.2 (the "License"); You may not use this file
#  except in compliance with the License. You may obtain a copy of the
#  License at http://gridengine.sunsource.net/Gridengine_SISSL_license.html
#
#  Software provided under this License is provided on an "AS IS" basis,
#  WITHOUT WARRANTY OF ANY KIND, EITHER EXPRESSED OR IMPLIED, INCLUDING,
#  WITHOUT LIMITATION, WARRANTIES THAT THE SOFTWARE IS FREE OF DEFECTS,
#  MERCHANTABLE, FIT FOR A PARTICULAR PURPOSE, OR NON-INFRINGING.
#  See the License for the specific provisions governing your rights and
#  obligations concerning the Software.
#
#  The Initial Developer of the Original Code is: Sun Microsystems, Inc.
#
#  Copyright: 2001 by Sun Microsystems, Inc.
#
#  All Rights Reserved.
#
##########################################################################
#___INFO__MARK_END__
#
# Credit should be given to Robert Manchek manchek@CS.UTK.EDU for creating
# a similar aimk procedure for the PVM 3.x distribution, Jul 22 1991,
# which inspired the Grid Engine aimk build scheme.

# Make sure to avoid confusion between shell variables and environment
# variables
#
set noglob
unsetenv buildarch BUILDARCH COMPILE_ARCH COMPILE_DIR LD_LIBRARY_PATH
set COMPILE_ARCH = ""
set COMPILE_DIR  = ""
set buildarch    = UNKNOWN

if ( -f dist/util/arch ) then
   set buildarch = `dist/util/arch`
else
   echo script dist/util/arch not available
   exit 1
endif

expr $buildarch : UNSUPPORTED > /dev/null
if ( $status == 0 ) then
   echo script dist/util/arch does not support \"$buildarch\"
   exit 1
else if ( $buildarch == UNKNOWN ) then
   echo script dist/util/arch does not recognize your operating systems
   exit 1
endif

# Map buildarch into uppercase BUILDARCH
#
switch ($buildarch)
case "aix43":
   set BUILDARCH = AIX43
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "aix51":
   set BUILDARCH = AIX51
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "cray":
   set BUILDARCH = UNICOS
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "crayts":
   set BUILDARCH = UNICOS_TS
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "craytsieee":
   set BUILDARCH = UNICOS_TS_IEEE
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "darwin":
   set BUILDARCH = DARWIN
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "fbsd-alpha"
   set BUILDARCH = FREEBSD_ALPHA
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "fbsd-amd64"
   set BUILDARCH = FREEBSD_AMD64
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "fbsd-i386"
   set BUILDARCH = FREEBSD_I386
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "fbsd-ia64"
   set BUILDARCH = FREEBSD_IA64
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "fbsd-ppc"
   set BUILDARCH = FREEBSD_PPC
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "fbsd-sparc64"
   set BUILDARCH = FREEBSD_SPARC64
   breaksw
case "hp10":
   set BUILDARCH = HP10
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "hp11":
   set BUILDARCH = HP11
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "hp11-64":
   set BUILDARCH = HP1164
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "irix65":
   set BUILDARCH = IRIX65
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "lx22-alpha":
   set BUILDARCH = ALINUX
   set COMPILE_ARCH = ALINUX_22
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "lx24-alpha":
   set BUILDARCH = ALINUX
   set COMPILE_ARCH = ALINUX_24
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "lx26-alpha":
   set BUILDARCH = ALINUX
   set COMPILE_ARCH = ALINUX_26
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "lx24-amd64":
   set BUILDARCH = LINUXAMD64
   set COMPILE_ARCH = LINUXAMD64_24
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "lx26-amd64":
   set BUILDARCH = LINUXAMD64
   set COMPILE_ARCH = LINUXAMD64_26
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "lx24-ia64":
   set BUILDARCH = LINUXIA64
   set COMPILE_ARCH = LINUXIA64_24
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "lx26-ia64":               
   set BUILDARCH = LINUXIA64
   set COMPILE_ARCH = LINUXIA64_26
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "lx22-sparc":
   set BUILDARCH = SLINUX
   set COMPILE_ARCH = SLINUX_22
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "lx24-sparc":
   set BUILDARCH = SLINUX
   set COMPILE_ARCH = SLINUX_24
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "lx26-sparc":
   set BUILDARCH = SLINUX 
   set COMPILE_ARCH = SLINUX_26
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "lx22-x86":
   set BUILDARCH = LINUX86
   set COMPILE_ARCH = LINUX86_22
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "lx24-x86":
   set BUILDARCH = LINUX86
   set COMPILE_ARCH = LINUX86_24
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "lx26-x86":
   set BUILDARCH = LINUX86
   set COMPILE_ARCH = LINUX86_26
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "necsx4":
   set BUILDARCH = NECSX4
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "necsx5":
case "necsx6":
case "sx":
   set BUILDARCH = NECSX5
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "sol-sparc":
   set BUILDARCH = SOLARIS
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "sol-sparc64":
   set BUILDARCH = SOLARIS64
   set TARGET_BITS = TARGET_64BIT
   breaksw
case "sol-x86":
   set BUILDARCH = SOLARIS86
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "tru64":
   set BUILDARCH = ALPHA5
   set TARGET_BITS = TARGET_32BIT
   breaksw
case "Interix-x86-3.5":
   set BUILDARCH = INTERIX
   set TARGET_BITS = TARGET_32BIT
   breaksw   
default:
   echo cannot compile - no mapping for architecture \"$buildarch\" defined yet
   exit 1
   breaksw
endsw

#---------------------------------------------------------------------------
# Preparsing for all args which change BUILDARCH or which exit
# all args here must be read in the main parsing loop to ensure
# they are eaten from the command line.
# All variables which are set above in the switch-case must be reset here
# e.g. BUILDARCH, TARGET_BITS, buildarch ...
#
set i = 1
while ($i <= $#argv)
   switch ("$argv[$i]")
   case "-32":
      set TARGET_BITS = TARGET_32BIT
      if ($BUILDARCH == SOLARIS64) then
         set BUILDARCH = SOLARIS  
         set buildarch = sol-sparc
      else if ($BUILDARCH == HP1164) then
         set BUILDARCH = HP11
         set buildarch = hp11
      else
         echo "32-bit switch not supported for $buildarch"
         exit 1
      endif
      breaksw
   case "-64":
      set TARGET_BITS = TARGET_64BIT
      if ($BUILDARCH == HP11) then
         set BUILDARCH = HP1164
         set buildarch = hp11-64
      else
         echo "64-bit switch not supported for $buildarch"
         exit 1
      endif
      breaksw
   case "-help":
   case "--help":
      goto usage
      breaksw
   endsw
   set i = `expr $i + 1`
end

# Source variables for definition of site dependent paths of software here
#
if ( -f aimk.site ) then
   source aimk.site
endif

# your private command line args go here
#
if ( -f aimk.private ) then
   source aimk.private
endif

goto start

usage:
   echo "usage: aimk <OPTIONS>"
   echo "OPTIONS are: "
   echo ""
   echo "-32               -> create 32 bit binaries (where supported)"
   echo "-64               -> create 64 bit binaries (where supported)"
   echo "-D*               -> include a macro definition in CFLAGS"
   echo "-add-copyright    -> insert a special copyright string in each binary"
   echo "-add-sun-copyright-> insert a Sun copyright string in each binary" 
   echo "-adoc             -> create source code documentation in adoc format"
   echo "-catman           -> generate man pages in catman format"
   echo "-cc               -> use cc/CC to compile c/c++ code"
   echo "-dce              -> enable dce security"
   echo "-debug            -> compile with debug information"
   echo "-efence           -> compile in debug mode and detect memory leaks"
   echo "-f                -> specify other Makefile instead of common/Makefile"
   echo "-gcc              -> use gcc instead of system compiler (where supported)"
   echo "-gettext          -> build the gettext libraries"
   echo "-gprof            -> enable gprof profiling"
   echo "-gcov             -> enable gcov code coverage testing"
   echo "-gss              -> enable gss security"
   echo "-help             -> show this help"
   echo "-htmlman          -> generate man and html pages"
   echo "-htmlmankv        -> generate man and html pages checked out with '-kv' flag"
   echo "-insure           -> compile in debug mode and detect memory leaks)"
   echo "-intl             -> compile with internationalization library (libintl)"
   echo "-java             -> compile Java sources such as DRMAA Java binding"
   echo "-javadoc          -> create JavaDoc for Java sources such as DRMAA Java binding"
   echo "-kerberos         -> enable kerberos authentication"
   echo "-man              -> generate man pages"
   echo "-mankv            -> generate man pages checked out with '-kv' flag"
   echo "-mo               -> generate the message catalogues (Solaris only)"
   echo "-no-core          -> don't compile the core system"
   echo "-no-intl          -> do not compile with internationalization library"
   echo "-no-mk            -> only return the specific architecture string"
   echo "-no-opt           -> compile with low optimization"
   echo "-no-qmake         -> don't compile qmake"
   echo "-no-qmon          -> don't compile qmon 3rdparty libs and qmon client"
   echo "-no-qtcsh         -> don't compile qtcsh"
   echo "-no-remote        -> don't compile 3rdparty remote module (rsh, rshd, rlogin)"
   echo "-no-rmon          -> disable rmon debug output"
   echo "-no-secure        -> diable ssl security (enabled by default)"
   echo "-only-core        -> build only the core system"
   echo "-only-depend      -> only compile 3rdparty/sge_depend"
   echo "-only-qmake       -> build only qmake"
   echo "-only-qmon        -> build only qmon 3rdparty libs and qmon client"
   echo "-only-qtcsh       -> build only qtcsh"
   echo "-only-remote      -> build only 3rdparty remote module (rsh, rshd, rlogin)"
   echo "-parallel n       -> spawn multiple make processes by passing option -j n to make"
   echo "-perlgui          -> build the experimental perlgui"
   echo "-po               -> merge gridengine .pot with translations (Linux)"
   echo "-pot              -> extract message template to gridengine .pot file (Linux)"
   echo "-purify           -> instrument code with purify (implies -debug)"
   echo "-qmake            -> use qmake instead of make"
   echo "-shared-libs      -> create libraries as shared libs"
   echo "-spool-berkeleydb -> use berkeleydb spooling"
   echo "-spool-classic    -> use classic flatfile spooling"
   echo "-spool-targets    -> only create qmaster, spooldefaults and spoolinit"
   echo "-sunman           -> create man pages for Sun branded man pages (with -man[kv])"
   echo "-umap             -> compile with usermapping and hostgroup feature"
  exit
}

start:

#-----------------------------------------------------------------------------------
# Variables below define default behavior and default compiler and linker
# flags which are passed to make
#

# Master settings
set SOURCE           = `pwd`
set SDIR             = ".."
set MSDIR            = ""
set MAKEFILE         = "${SDIR}/common/Makefile.global"

# SGE_INPUT_CFLAGS will be taken from environment and added to CFLAGS
if ( $?SGE_INPUT_CFLAGS ) then
   set INP_CFLAGS    = "$SGE_INPUT_CFLAGS"
else
   set INP_CFLAGS    = ""
endif

# Default build targets for which we have seperate make files
set SGE_DEPEND       = 0
set QMON             = 1
set CORE             = 1
set REMOTE           = 1
set QMAKE            = 1
set QTCSH            = 1
set MAKESTRPTIME     = 0
set MAKEFNMATCH      = 0
set MAKEWINGRIDLIB   = 0
set WITH_USERMAPPING = 0 # Compile without usermapping feature

# Default Flags and Libraries 
set OPTIMIZATION     = 1
set GPROFFED         = 0
set GCOVED           = 0
set DEBUGGED         = 0
set COMPILE_DC       = 0
set CC               = cc
set CXX              = CC
set CFLAGS           = "-D$TARGET_BITS -DQCONF_FLATFILE $INP_CFLAGS"
set CORE_INCLUDE     = "-I../security/sec -I../common -I../libs -I../libs/uti -I../libs/gdi -I../libs/japi -I../libs/sgeobj -I../libs/cull -I../libs/rmon -I../libs/comm -I../libs/comm/lists -I../libs/sched -I../libs/evc -I../libs/evm -I../libs/mir -I../libs/lck -I../daemons/common -I../daemons/qmaster -I../daemons/execd -I../daemons/schedd -I../clients/common -I."
set DEBUG_FLAG       = ""
set INSURE_FLAG      = ""
set NOERR_CFLAG      = ""
set NOERR_PROTO      = ""
set MEMCFLAG         = ""
set JAVA_ARCH        = ""
set JAVA             = 0

set LFLAGS           = "-L."
set XLFLAGS          = ""
set XLIBS            = ""
set LIBS             = "-lm -lpthread"
set LD_WRAPPER       = ""
set DLLIB            = "-ldl"
set LOADAVGLIBS      = ""
set TPLIB            = ""  # 3rdparty libs
set SLIBS            = ""

# Default settings for shared library creation
set SHAREDLIBS       = 0
set SHAREDEXT        = ".so"
set SHAREDLD         = ""
set SHARED_CFLAGS    = ""
set SHARED_LFLAGS    = ""

# Flags for man page creation, copyright stuff and adoc
set BUILDMAN          = 0
set BUILDCATMAN       = 0
set BUILDGETTEXT      = 0
set BUILDHTMLMAN      = 0
set BUILDJAVADOC      = 0
set SGE_PRODUCT_MODE  = ge
set ADD_COPYRIGHT     = 0
set ADD_SUN_COPYRIGHT = 0
set ADOC              = 0

# Default name and command line flags for some build commands
set MAKE              = make
set MAKEOPT           = ""
set STRIP             = strip
set RANLIB            = echo
set AR                = ar
set ARFLAGS           = rscv
set PARALLEL          = ""  
set USE_QMAKE         = 0

# I18N, L10N stuff
set GETTEXTBASE       = "$SOURCE/3rdparty/gettext"
set MESSAGES          = "../dist/locale/gridenginenotuniq.pot"
set MESSAGES_BIN      = "gridengine.mo"
set CREATE_MSG        = 0
set CREATE_MSG_DIR    = ../dist/locale
set CREATE_MSG_LANG   = "de en"

# These targets are more difficult to port - not all platforms support these
set INTERACTTARGETS   = "qmake qtcsh"

# SECURE
set SEC               = 1
set SECCLEAN          = sec_clean
set SECDEPEND         = sec_depend
set SECFLAGS          = "-DSECURE -I$OPENSSL_HOME/include"
set SECLIB            = -lsec
set SECLIBS           = libsec.a
set SECLIBOBJS        = sec_lib.o
set SECTARGETS        = sec_all
set KLFLAGS           = "-L$OPENSSL_HOME/lib"

# GSS
set GSSDEPEND         = ""
set GSSCLEAN          = ""
set GSSLIBS           = ""

# KERBEROS
set KRB	              = 0
set KRBTARGETS        = ""
set KRBDEPEND         = ""
set KRBCLEAN          = ""
set KRBLIBS           = ""
set KRBLIB            = ""
set KLIBS             = ""

# PERLGUI
set PERLGUI           = ""
set PERLGUI_CLEAN     = ""

# SPOOLING - default method is dynamic spooling, 
# spooling methods berkeleydb and classic
set SPOOLING_METHOD = "dynamic"
set SPOOLING_LIBS   = "-lspoolloader -lspoold -lspool"
set SPOOLING_DEPS   = "spoolloaderlib spooldlib spoollib spoolclib spoolblib"
# we generate a berkeley db shared lib, so we need bdb include and lib path
set CFLAGS            = "-I$BERKELEYDB_HOME/include/${BDB_INCLUDE_SUBDIR} $CFLAGS"
set LFLAGS            = "-L$BERKELEYDB_HOME/lib/${BDB_LIB_SUBDIR} $LFLAGS"
set SHARED_LFLAGS    = "$SHARED_LFLAGS -L$BERKELEYDB_HOME/lib"

# Compile with gettext and libintl - we have platform dependent defaults
switch ($BUILDARCH) 
   case LINUX86:
   case LINUXAMD64:
   case SOLARIS:
   case SOLARIS64:
   case SOLARIS86:
      set WITH_GETTEXT = 1
      breaksw
   default:
      set WITH_GETTEXT = 0
      breaksw
endsw      

# X specific stuff (Xmt, Xbae ...)
#
# This stuff is for building the Xmt library
# If possible (i.e. imake is installed correctly)
# use the Imakefile (change Xmt.tmpl)
#
set XINCD                = "-I../3rdparty/qmon/Xbae -I../3rdparty/qmon/iconlist -I../3rdparty/qmon/ltree -I../3rdparty/qmon/outline -I../3rdparty/qmon/rowcol -I../3rdparty/qmon/spinbox -I../clients/qmon -I../3rdparty/qmon/tab"
if ( $?MY_XINCD ) then
   set XINCD             = "-I${MY_XINCD} ${XINCD}"
endif
set XMTINCD  = "-DHAVE_MEMMOVE -DXMT_HAS_STRERROR -I../3rdparty/qmon/Xmt310"
set XmtFileRootDir       = ""
set XmtFileSearchPath    = ""
# set   REPLAYXT_SUPPORT = 0
#
# The following DEFINES are necessary in special cases
# for more information see Xmt.tmpl
# -DNO_EDITRES       if libXmu is not available (HP)
# -DXMSTRING_TO_COMPOUND_TEXT_BUG  if tearoff menus dump core define this
# -DBACKSPACEBUG     if BackSpace in a Cli widget dumps core define this
# -DIBM_MANAGE_WORKAROUND     IBM/RS6000(AIX 3.2) if children of XmtLayout
#                             widget show up in upper left corner
# -DMISSING_METHOD_TYPES      for an old version of Motif 1.1
#                             (XmMenuProc not in XmP.h == old)
# -DXMTDEFAULTCONFIGDIR=\"$(XmtFileRootDir)\"   normally the home directory
# -DXMTFILESEARCHPATH=\"$(XmtFileSearchPath)\"  normally the home directory

unset nomk

set found
while ($#argv >= 1 && $?found)
   switch ("$argv[1]")
   # the following flags must be skipped
   case -32:
   case -64:
   case -help:
   case --help:
      breaksw
   case "-add-copyright"
      set ADD_COPYRIGHT = 1
      breaksw
   case "-add-sun-copyright"
      set ADD_SUN_COPYRIGHT = 1
      breaksw
   case "-adoc":
      set CORE   = 0
      set QMAKE  = 0
      set QMON   = 0
      set QTCSH  = 0
      set REMOTE = 0
      set ADOC = 1
      breaksw
   case "-catman":
      set BUILDCATMAN = "1"
      breaksw
   case "-cc"
      set CC       = cc
      set CXX      = CC
      breaksw
   case "-crypto":
      set CFLAGS  = "-DCRYPTO -I$OPENSSL_HOME/include $CFLAGS"
      set LFLAGS  = "-L$OPENSSL_HOME/lib $LFLAGS"
      switch ($BUILDARCH)
         case HP11:
            set KLIBS = "-lcrypto"
            breaksw
         default:
            set CFLAGS = "-DLOAD_OPENSSL $CFLAGS"
            set KLIBS = ""
      endsw
      breaksw
   case "-D*":
      set CFLAGS = "$argv[1] $CFLAGS"
      breaksw
   case "-dce":
      set LIBS = " -ldce -threads $LIBS"
      set CFLAGS = " -DDCE -I/usr/include/dce $CFLAGS"
      breaksw
   case "-debug":
      set DEBUG_FLAG = "$DEBUG_FLAG -g"
      set DEBUGGED   = 1
      set STRIP      = "echo"
      breaksw
   case "-f":
      set argv   = ($argv[2-])
      if ($#argv >= 1) then
         set first_char = `echo $argv[1] | cut -c1`
         if ( "$first_char" == "/") then
            set MAKEFILE = $argv[1]
         else
            set MAKEFILE = "../$argv[1]"
         endif
         unset first_char
         set MSDIR = `dirname ${MAKEFILE}`
      else
         echo "usage: aimk [valid args] -f <makefile> [valid args]"
         exit 1
      endif
      breaksw
   case "-gcc"
      set CC       = gcc
      set CXX      = g++
      breaksw
   case "-gettext"
      set BUILDGETTEXT = 1
      breaksw
   case "-gprof":
      set DEBUG_FLAG = "$DEBUG_FLAG -pg"
      set GPROFFED   = 1
      set STRIP      = "echo"
      breaksw
   case "-gcov":
      set GCOVED     = 1
      set STRIP      = "echo"
      breaksw
   case "-gss":
      set GSSDEPEND = "gss_depend"
      set GSSCLEAN = "gss_clean"
      set CFLAGS  = "-DKERBEROS -I../security/gss -I$KRB_HOME/include -I$KRB_HOME/include/gssapi $CFLAGS"
      set GSSLIBS = "-lgssapi_krb5 -lkrb5 -lk5crypto -lcom_err"
      breaksw
   case "-htmlman":
      set BUILDMAN = "1"
      set BUILDHTMLMAN = "1"
      set KVMAN = 0
      breaksw      
   case "-htmlmankv":
      set BUILDMAN = "1"
      set BUILDHTMLMAN = "1"
      set KVMAN = 1
      breaksw
   case "-insure"
      set CC         = insure
      set CXX        = insure
      #set DEBUG_FLAG = "$DEBUG_FLAG -g -Zsl -Zvm -Zvm -Zkt"
      #set DEBUG_FLAG = "$DEBUG_FLAG -g -Zsl"
      #set DEBUG_FLAG = "$DEBUG_FLAG -Zsl"
      set INSURE_FLAG = "-Zsl -Zvm"
      set LFLAGS     = "-Zsl $LFLAGS"
      set DEBUGGED   = 1
      breaksw
   case "-efence"
      set LFLAGS     = "-lefence $LFLAGS"
      set DEBUGGED   = 1
      breaksw
   case  "-intl"
      set WITH_GETTEXT = 1
      breaksw
   case  "-java"
      set JAVA = 1
      breaksw
   case  "-javadoc"
      set BUILDJAVADOC = 1
      breaksw
   case "-kerberos":
      set KRB        = 1
      set KRBTARGETS = ""
      set KRBDEPEND  = "krb_depend"
      set KRBCLEAN   = "krb_clean"
      set KRBLIBS    = "libkrb.a"
      set KRBLIB     = "-lkrb"
      set CFLAGS     = "-DKERBEROS -I../security/krb -I$KRB_HOME/include -I$KRB_HOME/include/gssapi $CFLAGS"
      set KLFLAGS    = "-L$KRB_HOME/lib"
      set KLIBS      = "-lkrb5 -lk5crypto -lcom_err -lresolv"
      breaksw
   case "-man":
      set BUILDMAN   = 1
      set KVMAN      = 0
      breaksw
   case "-mankv":
      set BUILDMAN   = 1
      set KVMAN      = 1
      breaksw
   case  "-mo"
      echo "BUILDARCH=$BUILDARCH"
      if ( $BUILDARCH == sol-sparc || $BUILDARCH == sol-sparc64 ) then
         scripts/msgmerge.csh
      else
         echo "Use a Solaris machine"
      endif
      exit 0
      breaksw
   case "-no-core":
      set CORE = 0
      breaksw
   case  "-no-intl"
      set WITH_GETTEXT = 0
      breaksw
   case "-no-mk":
      set nomk
      breaksw
   case "-no-opt":
      set OPTIMIZATION = 0
      breaksw
   case "-no-qmake":
      set QMAKE = 0
      breaksw
   case "-no-qmon":
      set QMON = 0
      breaksw
   case "-no-qtcsh":
      set QTCSH = 0
      breaksw
   case "-no-remote":
      set REMOTE = 0
      breaksw
   case "-no-rmon":
      set CFLAGS  = "-DNO_SGE_COMPILE_DEBUG $CFLAGS"
      setenv RMONLIB  ""
      breaksw
   case "-no-secure":
      set SEC         = 0
      set SECCLEAN    = ""  
      set SECDEPEND   = ""
      set SECFLAGS    = ""
      set SECLIB      = ""
      set SECLIBS     = ""
      set SECLIBOBJS  = ""
      set SECTARGETS  = ""
      set KLFLAGS     = ""
      breaksw
   case "-only-core":
      set CORE   = 1
      set QMAKE  = 0
      set QMON   = 0
      set QTCSH  = 0
      set REMOTE = 0
      breaksw
   case "-only-depend":
      set CORE       = 0
      set QMAKE      = 0
      set QMON       = 0
      set QTCSH      = 0
      set REMOTE     = 0
      set SGE_DEPEND = 1
      breaksw
   case "-only-qmake":
      set CORE   = 0
      set QMAKE  = 1
      set QMON   = 0
      set QTCSH  = 0
      set REMOTE = 0
      breaksw
   case "-only-qmon":
      set CORE   = 0
      set QMAKE  = 0
      set QMON   = 1
      set QTCSH  = 0
      set REMOTE = 0
      breaksw
   case "-only-qtcsh":
      set CORE   = 0
      set QMAKE  = 0
      set QMON   = 0
      set QTCSH  = 1
      set REMOTE = 0
      breaksw
   case "-only-remote":
      set CORE   = 0
      set QMAKE  = 0
      set QMON   = 0
      set QTCSH  = 0
      set REMOTE = 1
      breaksw
   case "-parallel":
      set argv     = ($argv[2-])
      set PARALLEL = "-j $argv[1]"
      breaksw
   case "-perlgui":
      set PERLGUI = "perlgui_all"
      set PERLGUI_CLEAN = "perlgui_clean"
      breaksw
   case  "-po"
      if ( $buildarch == lx24-x86 ) then
         scripts/msgmerge.csh merge
         echo ""
         echo "==========READY FOR TRANSLATION========="
         echo ""
      else
         echo "Must be done on Linux"
         exit 1
      endif
      exit 0
      breaksw
   case  "-pot"
      if ( $buildarch !=  ) then
         echo "Must be done on lx24-86 architecture"
         exit 1
      endif   
      rm -f dist/locale/gridenginenotuniq.pot
      rm -f dist/locale/gridengine.pot
      $0 -intl -only-core messages
      echo "Adding script messages"
      setenv BUILDARCH $BUILDARCH
      foreach f ( dist/inst_sge  dist/util/setfileperm.sh dist/util/arch_variables \
                  dist/util/qhold.sh dist/util/sgeCA/sge_ca )
         set pf = "/tmp/`basename $f`.pot"
         $TCLSH scripts/infotext_msg_parse.tcl -F $pf $f
         echo "Appending script pot file $pf" 
         cat $pf >> dist/locale/gridenginenotuniq.pot
         rm $pf
      end
      echo "Uniqing pot file"
      scripts/msgmerge.csh merge
      exit 0
      breaksw
   case "-purify":
      set LD_WRAPPER = "purify"
      set DEBUGGED   = 1
      breaksw
   case "-qmake"
      set USE_QMAKE    = 1
      set MAKE     = "qmake"
      set MAKEOPT = "-cwd -v PATH -pe make 2-100 -now n -- $MAKEOPT"
      breaksw
   case "-secure"
      echo "___________W_A_R_N_I_N_G_____________"
      echo "The -secure option is no longer supported as security is now enabled"
      echo "by default.  Please remove the -secure option from your aimk.site and"
      echo "aimk.private files.  To disable security, you may use the -no-secure"
      echo "option."
      echo
      breaksw
   case "-shared-libs":
      set SHAREDLIBS = 1
      breaksw
   case "-spool-berkeleydb":
      set SPOOLING_METHOD   = "berkeleydb"
      set SPOOLING_LIBS     = "-lspoolloader -lspoolb -lspool -ldb${BDB_LIB_SUFFIX}"
      set SPOOLING_DEPS     = "spoolloaderlib spoolblib spoollib"
      breaksw
   case "-spool-classic":
      set SPOOLING_METHOD = "classic"
      set SPOOLING_LIBS   = "-lspoolloader -lspoolc -lspool"
      set SPOOLING_DEPS   = "spoolloaderlib spoolclib spoollib"
      breaksw
   case "-classic-targets":   
      set CORE   = 1
      set QMAKE  = 0
      set QMON   = 0
      set QTCSH  = 0
      set REMOTE = 0
      set SPOOLING_METHOD = "classic"
      set SPOOLING_LIBS   = "-lspoolloader -lspoolc -lspool"  
      set SPOOLING_DEPS   = "spoolloaderlib spoolclib spoollib"
      set argv = ($argv sge_qmaster spoolinit spooldefaults)
      if ("$COMPILE_ARCH" != "") then
         set COMPILE_DIR = ${COMPILE_ARCH}_$SPOOLING_METHOD
      else
         set COMPILE_DIR = ${BUILDARCH}_$SPOOLING_METHOD
      endif 
      breaksw
   case "-sunman":
      set SGE_PRODUCT_MODE=sge
      breaksw
   case  "-umap"
      set WITH_USERMAPPING = 1
      breaksw
   default:
      unset found
      continue
      breaksw
   endsw
   set argv     = ($argv[2-])
end

if ( $DEBUGGED && $USE_QMAKE ) then
   set MAKEOPT = "-verbose $MAKEOPT"
endif

if ( $JAVA == 1 || $BUILDJAVADOC == 1 ) then
   # Make sure we can find JAVA_HOME
   if ( ${?JAVA_HOME} == 0 ) then
      echo "Please set JAVA_HOME"
      exit
   else
      if ($JAVA == 1) then
         set CORE_INCLUDE = "$CORE_INCLUDE -I${JAVA_HOME}/include"
      endif
   endif
endif

set CFLAGS = "$CFLAGS -DSPOOLING_$SPOOLING_METHOD $SECFLAGS"

#---------------------------------------------------------------------------

if ($SEC == 1) then
   switch ($BUILDARCH)
    case HP11:
      set KLIBS = "-lcrypto"
      breaksw
   default:
      set CFLAGS = "-DLOAD_OPENSSL $CFLAGS"
      set KLIBS = ""
   endsw
endif

#---------------------------------------------------------------------------
if ("$COMPILE_ARCH" == "") then
   set COMPILE_ARCH = $BUILDARCH
endif

if ($?nomk) then
   echo $COMPILE_ARCH
   exit 0
endif

if (! -d $COMPILE_ARCH) then
   echo " INFO: Architecture dir $COMPILE_ARCH not existing. Making"
   mkdir $COMPILE_ARCH
endif

if ( "$COMPILE_DIR" != "" ) then
   if (! -d $COMPILE_DIR) then
      mkdir $COMPILE_DIR
   endif
endif

switch ($BUILDARCH)
case AIX43:
case AIX51:
   if ( $OPTIMIZATION == 1 ) then
      set OFLAG = "-O3 -qstrict -qmaxmem=8192"
   else
      set OFLAG    = "-O -qmaxmem=8192"
   endif
   set CC       = xlc_r
   set CXX      = xlC_r
   set CFLAGS   = "$OFLAG -DAIX -D$BUILDARCH -D_LARGE_FILES -DUSE_STDARG -DGETHOSTBYADDR_R5 -DGETHOSTBYNAME_R3 $DEBUG_FLAG $CFLAGS -bnoquiet"
   set DEPEND_FLAGS = "$CFLAGS $XMTINCD"
   set XMTDEF   = ""
   set XINCD    = "$XMTINCD $XINCD"
   set XCFLAGS  = "-bnodelcsect -DNeedFunctionPrototypes -DNeedVarargsPrototypes $XMTDEF $XINCD"
   set XLIBD    = ""
   set XLFLAGS  = "$XLIBD"
   set XLIBS    = "-lXm  -lXt -lXext -lX11 -lm -liconv -lc"

   set SHAREDLD = "$SOURCE/scripts/ldAix"
   set SHARED_CFLAGS = ""
   set SHARED_LFLAGS = "$LFLAGS $KLFLAGS -brtl -bhalt:4 -bM:SRE -bE:lib.exp -H512 -T512 -bnoentry"

   # add DEBUG_FLAGS to LFLAGS after SHARED_LFLAGS - -g isn't recognized by ld
   set LFLAGS   = "$DEBUG_FLAG -brtl $LFLAGS"
   set JAVA_ARCH = "aix"
   breaksw

case ALPHA5:
   set COMPILE_DC = 1

   if ( $OPTIMIZATION == 1 ) then
      set OFLAG = "-O3"
   else
      set OFLAG = "-O"
   endif

   set CXX = "cxx"
   set DLLIB = ""

   # gprof option is superset of debug option
   # -g3 produces better debugging information than -g1
   if ( $DEBUGGED == 1) then
      set DEBUG_FLAG = "-g2"
      set OFLAG      = "-O"
   endif

   if ( $GPROFFED == 1) then
      set DEBUG_FLAG = "-g1 -pg -gen_feedback -non_shared"
   endif

   set NOERR_CFLAG  = "-w"
   set CFLAGS       = "$OFLAG -w2 -std1 -portable -pthread -warnprotos -DALPHA -D$BUILDARCH $DEBUG_FLAG $CFLAGS -DGETHOSTBYNAME_R3 -DGETHOSTBYADDR_R5"
   set DEPEND_FLAGS = "$CFLAGS $XMTINCD"

   set SHAREDLD      = $CC
   set SHARED_CFLAGS = ""
   set SHARED_LFLAGS = "$SHARED_LFLAGS -L. -shared -pthread"

   set LFLAGS   = "-pthread $DEBUG_FLAG $LFLAGS"
   set XMTDEF   = ""
   set XINCD    = "$XMTINCD $XINCD"
   set XCFLAGS  = "-w -DFUNCPROTO=3 -DDEC -DSMT $XMTDEF $XINCD"
   set XLIBD    = ""
   set XLFLAGS  = "-taso $XLIBD"
   set XLIBS    = "-lXm -lXt -lXext -lX11 -lSM -lICE -lPW -ldnet_stub"
   set LIBS     = "-lpset -lmach $LIBS"
   set JAVA_ARCH = "alpha"
   breaksw

case DARWIN:
   set COMPILE_DC = 1

   if ( $OPTIMIZATION == 1 ) then
      set OFLAG = "-O3"
   else
      set OFLAG = "-O"
   endif

   set CC = "gcc"
   set CXX = "g++"
   set DLLIB = "-ldl"

   if ( $DEBUGGED == 1) then
      set DEBUG_FLAG = "-g"
      set OFLAG = ""
   endif

   if ( $GPROFFED == 1) then
      set DEBUG_FLAG = "$DEBUG_FLAG -pg"
   endif

   if ($GCOVED == 1) then
      set DEBUG_FLAG = "$DEBUG_FLAG -fprofile-arcs -ftest-coverage"
   endif

   set REL = `/usr/bin/uname -r`
   switch ($REL)
   case "6.*":
      set CFLAGS = "$OFLAG -no-cpp-precomp -flat_namespace -Wall -Werror -Wstrict-prototypes -DDARWIN -DDARWIN6 -DGETHOSTBYNAME -DGETHOSTBYADDR_M $DEBUG_FLAG $CFLAGS"
      breaksw
   case "7.*":
      set CFLAGS = "$OFLAG -no-cpp-precomp -flat_namespace -Wall -Werror -Wstrict-prototypes -DDARWIN -DDARWIN7 -DGETHOSTBYNAME -DGETHOSTBYADDR_M $DEBUG_FLAG $CFLAGS"
      breaksw
   default:
      echo DARWIN release \"$REL\" not supported!
      exit 1
      breaksw
   endsw

   set DEPEND_FLAGS = "$CFLAGS $XMTINCD"
   set LFLAGS   = "$DEBUG_FLAG $LFLAGS"

   set SHAREDLD = "$CC -dynamiclib -flat_namespace -undefined warning"
   set SHARED_CFLAGS = "-dynamic"
   set SHARED_LFLAGS = "$SHARED_LFLAGS -L."
   set SHAREDEXT = ".dylib"
   if ( $SHAREDLIBS == 1 ) then
      set libext = $(SHAREDEXT)
   else
      set libext = ".a"
   endif   
   
#   if ( -r "/sw/lib/libxm$libext" ) then
#      set MOTIFHOME = /sw
#   else
#      set MOTIFHOME = /usr/local
#   endif   
   set MOTIFHOME = /usr/X11R6

   set XMTDEF   = ""
   set XMTINCD  = "$XMTINCD -I$MOTIFHOME/include"
   set XINCD    = "$XMTINCD $XINCD -I/usr/X11R6/include"
   set XCFLAGS  = "-DNeedFunctionPrototypes -DNeedVarargsPrototypes -Wno-error $XMTDEF $XINCD"
   set XLIBD    = "-L$MOTIFHOME/lib -L/usr/X11R6/lib"
   set XLIBS    = "-lXm -lXpm -lXt -lXext -lX11 -lSM -lICE"

   switch ($REL)
   case "6.*":
      set XLFLAGS = "-bind_at_load $XLIBD"
      breaksw
   case "7.*":
      set XLFLAGS = "$XLIBD"
      breaksw
   default:
      echo DARWIN release \"$REL\" not supported!
      exit 1
      breaksw
   endsw

   set NOERR_CFLAG = "-Wno-error"
   set NOERR_PROTO = "-Wno-strict-prototypes"
   set JAVA_ARCH = ""
   unset REL
   breaksw

case FREEBSD_*:
   set COMPILE_DC = 1
   set MAKE = gmake
   set OFLAG = "-O"
   set ARFLAGS     = rcv
   set CC       = gcc
   set CXX      = g++
   set DLLIB	= ""
   set DEPEND_FLAGS = "$CFLAGS $XMTINCD"
   set PTHRDSFLAGS = "-D_REENTRANT -D__USE_REENTRANT"
   set CFLAGS   = "$OFLAG -Wall -DFREEBSD -D$BUILDARCH -DGETHOSTBYNAME_M -DGETHOSTBYADDR_M $DEBUG_FLAG $CFLAGS -I/usr/X11R6/include"
   set NOERR_CFLAG = "-Wno-error"
   set LFLAGS   = "$DEBUG_FLAG $LFLAGS"
   set LIBS     = "$LIBS -lkvm"
   set SHAREDLD = $CC
   set SHARED_CFLAGS = "-fPIC"
   set SHARED_LFLAGS = "$SHARED_LFLAGS -L. -shared" 
   
   set XMTDEF   = ""
   set XINCD    = "$XMTINCD $XINCD -I/usr/X11/include"
   set XCFLAGS  = "-Wno-strict-prototypes -Wno-error $XMTDEF $XINCD"
   set XLIBD    = "-L/usr/X11R6/lib"
   set XLFLAGS  = "$XLIBD"
   set XLIBS    = " -Xlinker -lXm -Xlinker -Bdynamic -lXpm -lXt -lXext -lX11 -lSM -lICE -lXp"
   set RM	= "rm -f"
   set JAVA_ARCH = ""
   breaksw 

case IRIX65:
   if ( $OPTIMIZATION == 1 ) then
      set OFLAG = "-O3"
   else
      set OFLAG = "-O"
   endif

   if ( $DEBUGGED == 1 ) then
      set OFLAG = ""
   endif 

   set DLLIB = ""

   set COMPILE_DC = 1
   set MAKE = gmake

   set CFLAGS   = "$OFLAG -n32 -mips3 -DIRIX -D$BUILDARCH -DGETHOSTBYNAME_M -DGETHOSTBYADDR_M $DEBUG_FLAG $CFLAGS"
   set DEPEND_FLAGS = "$CFLAGS $XMTINCD"
   set LFLAGS   = "-n32 -mips3 $DEBUG_FLAG $LFLAGS"
   set LIBS     = "$LIBS"
   set XMTDEF   = ""
   set XINCD    = "$XMTINCD $XINCD"
   set XCFLAGS  = "$XMTDEF $XINCD"
   set XLIBD    = ""
   set XLFLAGS  = "$XLIBD"
   set XLIBS    = "-lXm -lXt -lXext -lX11 -lPW -lm"
   set RANLIB   = echo

   set SHAREDLD = ld
   set SHARED_CFLAGS = "-KPIC"
   set SHARED_LFLAGS = "$SHARED_LFLAGS -L. -shared"
   set JAVA_ARCH = "irix"
   breaksw

case HP11:
case HP1164:
   set MAKE = gmake
   set DLLIB = ""
   set OFLAG = ""
   if ($BUILDARCH == HP11) then
      set HOSTRESOLVEFLAGS = "-DGETHOSTBYNAME -DGETHOSTBYADDR"
      set MEMFLAG = +DD32
   else
      set HOSTRESOLVEFLAGS = "-DGETHOSTBYNAME -DGETHOSTBYADDR_M"
      set MEMFLAG = +DD64
   endif
   if ($CC == gcc) then
      if ( $DEBUGGED == 0 ) then
         if ( $OPTIMIZATION != 0 ) then
            set OFLAG = "-O2"
         endif
      endif
      set CFLAGS = "$OFLAG -Wall -Wstrict-prototypes -D$BUILDARCH $HOSTRESOLVEFLAGS $DEBUG_FLAG $CFLAGS"
   else
      if ( $DEBUGGED == 0 ) then
         if ( $OPTIMIZATION != 0 ) then
            set OFLAG = "+O2"
         endif
      endif
      set CXX = "aCC"
      set CFLAGS = "-Ae $MEMFLAG $OFLAG -DHPUX -D$BUILDARCH $HOSTRESOLVEFLAGS $DEBUG_FLAG $CFLAGS"
   endif

   set DEPEND_FLAGS = "$CFLAGS $XMTINCD"

   set SHAREDLD = ld
   if ( $CC == gcc ) then
      set SHARED_CFLAGS = "-fPIC"
      set SHARED_LFLAGS = "$SHARED_LFLAGS -L. -shared"
   else
      set SHARED_CFLAGS = "+z"
      set SHARED_LFLAGS = "$SHARED_LFLAGS -L. -b +s"
   endif

   set CFLAGS = "-D_LARGEFILE64_SOURCE -D_FILE_OFFSET_BITS=64 $CFLAGS -D_REENTRANT"
   set LIBS     = "-lnsl $LIBS"

   if ($BUILDARCH == HP11) then
      set LFLAGS   = "$DEBUG_FLAG $LFLAGS +DD32 -Wl,+s"
   else
      set LFLAGS   = "$DEBUG_FLAG $LFLAGS +DD64 -Wl,+s"
   endif

   set XMTDEF   = "-DNO_EDITRES"
   set XINCD    = "$XMTINCD $XINCD"
   set XCFLAGS  = "$XMTDEF $XINCD"
   set XLIBD    = ""
   set XLFLAGS  = "$XLIBD"
   set XLIBS    = "-lXm -lXt -lXext -lXhp11 -lX11 -lm"

   set SHAREDEXT = ".sl"
   set JAVA_ARCH = "hp-ux"
   breaksw
   
case ALINUX:
case LINUX86:
case LINUXAMD64:
case LINUXIA64:
   set COMPILE_DC = 1

   if ($OPTIMIZATION == 1) then
      set OFLAG = "-O3"
   else
      set OFLAG = "-O"
   endif

   if ($CC != insure) then
      set CC  = gcc
      set CXX = g++
   else
      set CFLAGS = "-Wno-error $CFLAGS"
      set OFLAG = "-O2"
      if ( $SEC ==  1 ) then
         set KLIBS = "$OPENSSL_HOME/lib/libcrypto.a.insure"
      endif
      set LIBS = "$LIBS"
   endif

   if ($DEBUGGED == 1) then
      set DEBUG_FLAG = "-g $INSURE_FLAG"
      set OFLAG = ""
   endif

   if ($GPROFFED == 1) then
      set DEBUG_FLAG = "$DEBUG_FLAG -pg"
   endif

   if ($GCOVED == 1) then
      set DEBUG_FLAG = "$DEBUG_FLAG -fprofile-arcs -ftest-coverage"
   endif

   if ( -f /lib/libc.so.6 ) then
      strings /lib/libc.so.6 | grep "GNU C Library" | grep '2.[234]' >& /dev/null
      if ($status == 0) then
         set CFLAGS = "-DHAS_IN_PORT_T $CFLAGS"
      endif
   else if ( -f /usr/include/netinet/in.h ) then
      grep typedef /usr/include/netinet/in.h | grep in_port_t >& /dev/null
      if ($status == 0) then
         set CFLAGS = "-DHAS_IN_PORT_T $CFLAGS"
      endif
   endif

   set CFLAGS = "$OFLAG -Wall -Werror -Wstrict-prototypes -DLINUX -D$BUILDARCH -D$COMPILE_ARCH -D_GNU_SOURCE -DGETHOSTBYNAME_R6 -DGETHOSTBYADDR_R8 $DEBUG_FLAG $CFLAGS"
   if ("$ADOC" == 1) then
      set CFLAGS = "$CFLAGS -Wno-error"
   endif
   set LFLAGS   = "$DEBUG_FLAG $LFLAGS"

   set SHAREDLD = $CC
   set SHARED_CFLAGS = "-fPIC"
   set SHARED_LFLAGS = "$SHARED_LFLAGS -L. -shared"

   if ( $SHAREDLIBS == 1 || $SPOOLING_METHOD == dynamic ) then
      set LFLAGS = "$LFLAGS -rdynamic"
   endif

   set NOERR_CFLAG = "-Wno-error"
   set NOERR_PROTO = "-Wno-strict-prototypes"
   set LIBS     = "$LIBS"

   set XMTDEF   = ""

   if ($TARGET_BITS == TARGET_32BIT || $BUILDARCH == ALINUX || $BUILDARCH == LINUXIA64) then
      set XLIBD    = "-L/usr/X11R6/lib"
   else
      set XLIBD    = "-L/usr/X11R6/lib64"
   endif

   set XLFLAGS  = "$XLIBD"

   set DEPEND_FLAGS = "$CFLAGS $XMTINCD"

   if ($BUILDARCH != ALINUX) then
      set XINCD    = "$XMTINCD $XINCD -I/usr/X11R6/include"
      set XCFLAGS  = "-Wno-strict-prototypes -Wno-error $XMTDEF $XINCD"
      set XLIBS    = "-lXm -lXpm -lXt -lXext -lXmu -lX11 -lSM -lICE -lXp"
   else
      set XINCD    = "$XMTINCD $XINCD"
      set XCFLAGS  = "-DNeedFunctionPrototypes -Wno-error $XMTDEF $XINCD"
      set XLIBS    = "-Xlinker -Bstatic -lXm -Xlinker -Bdynamic -lXpm -lXt -lXext -lX11 -lSM -lICE -lXp"
   endif
   set JAVA_ARCH = "linux"
   breaksw
   
case INTERIX:
   set COMPILE_DC     = 1
   set MAKEWINGRIDLIB = 1

   set CORE_INCLUDE = "$CORE_INCLUDE -I../libs/wingrid"
   #set LIBS = "$LIBS -lwingrid"

   if ($OPTIMIZATION == 1) then
      set OFLAG = "-O3"
   else
      set OFLAG = "-O"
   endif

   if ($CC == gcc) then
      set CC  = gcc
      set CXX = gcc
      #set CFLAGS = "-Wl,-EL,-b=pe-i386 -Wall -Werror -O0 -D$BUILDARCH -D$COMPILE_ARCH -DGETHOSTBYNAME_M -I/dev/fs/C/Program\ Files/Microsoft\ Visual\ Studio\ .NET\ 2003/Vc7/PlatformSDK/Include -DGETHOSTBYADDR_M $DEBUG_FLAG $CFLAGS"
      set CFLAGS = "-Wl,-EL,-b=pe-i386 -Wall -Werror -O0 -D$BUILDARCH -D$COMPILE_ARCH -DGETHOSTBYNAME_M -DGETHOSTBYADDR_M $DEBUG_FLAG $CFLAGS"      
   else
      set CC  = cc
      set CXX = cc
      set CFLAGS = "-X /Wall -X /WX $OFLAG -D$BUILDARCH -D$COMPILE_ARCH $DEBUG_FLAG $CFLAGS"
   endif

   if ($DEBUGGED == 1) then
      set DEBUG_FLAG = "-g $INSURE_FLAG"
      set OFLAG = ""
   endif
   if ($GPROFFED == 1) then
      set DEBUG_FLAG = "$DEBUG_FLAG -pg"
   endif

   if ( -f /lib/libc.so.6 ) then
      strings /lib/libc.so.6 | grep "GNU C Library" | grep '2.[234]' >& /dev/null
      if ($status == 0) then
         set CFLAGS = "-DHAS_IN_PORT_T $CFLAGS"
      endif
   endif

   set LFLAGS   = "$DEBUG_FLAG $LFLAGS"

   set SHAREDLD = $CC
#   set SHARED_CFLAGS = "-fPIC"
   set SHARED_LFLAGS = "-L. -shared"

   set NOERR_CFLAG = "-Wno-error"
   set NOERR_PROTO = "-Wno-strict-prototypes"
#   set LIBS     = "-lgcc -ldb -ll -lm -lpthread -lpsxdll -lc -lrpclib -lsocket -ly -lfrtbegin -litcl3.2 -litk3.2 -lg2c $LIBS"
   
      
   set XMTDEF   = ""

   if ($TARGET_BITS == TARGET_32BIT || $BUILDARCH == ALINUX) then
      set XLIBD    = "-L/usr/X11R6/lib"
   else
      set XLIBD    = "-L/usr/X11R6/lib64"
   endif

   set XLFLAGS  = "$XLIBD"

   set DEPEND_FLAGS = "$CFLAGS $XMTINCD"

   set XINCD    = "$XMTINCD $XINCD -I/usr/X11R6/include -I/usr/local/include/bind"
   set XCFLAGS  = "-Wno-strict-prototypes -Wno-error $XMTDEF $XINCD"
   set XLIBS    = "-lXm -lXpm -lXt -lXext -lXmu -lX11 -lSM -lICE -lXp"
   breaksw
   
case NECSX4:
#
# we need the '-hfloat0'-flag for CFLAGS and LFLAGS because the
# XDR-Libraray does not work properly for other floating point models
# float1 => (floating poit exceptions for value DBL_MAX during unpacking
# double values)
#
   set INTERACTTARGETS = ""
   set COMPILE_DC = 1
   set MAKE = make
   set CFLAGS = "-DNECSX4 -DGETHOSTBYNAME_M -DGETHOSTBYADDR_M -dir,novec,nopar,nopthr -hfloat0 $DEBUG_FLAG $CFLAGS"
   set DEPEND_FLAGS = "$CFLAGS $XMTINCD"
   set LFLAGS   = "-hfloat0 $DEBUG_FLAG $LFLAGS"
   set LIBS     = "$LIBS"
   set XMTDEF   = ""
   set XINCD    = ""
   set XCFLAGS  = "$XMTDEF $XINCD"
   set XLFLAGS  = ""
   if ( $SHAREDLIBS == 1 ) then
      echo creating shared libs not yet supported on $BUILDARCH
      exit 1
   endif
   set JAVA_ARCH = ""
   breaksw

case NECSX5:
   set INTERACTTARGETS = ""
   set COMPILE_DC = 1
   if ( $CORE == 1 ) then
      set MAKESTRPTIME = 1
      set MAKEFNMATCH = 1
   endif
   if ( $QTCSH == 1 ) then
      set MAKEFNMATCH = 1
   endif
   set MAKE = make
   set OFLAG = "-h1"
   set CFLAGS_NOOPT = "-DNECSX5 -DGETHOSTBYNAME_M -DGETHOSTBYADDR_M -D_REENTRANT -dir,novec,nopar,nopthr -hfloat0 $DEBUG_FLAG $CFLAGS"
   set CFLAGS = "$CFLAGS_NOOPT $OFLAG"
   set DEPEND_FLAGS = "$CFLAGS $XMTINCD"
   set LFLAGS   = "$DEBUG_FLAG $LFLAGS"
   set XMTDEF   = ""
   set XINCD    = ""
   set XCFLAGS  = "$XMTDEF $XINCD"
   set XLFLAGS  = ""
   if ( $SHAREDLIBS == 1 ) then
      echo creating shared libs not yet supported on $BUILDARCH
      exit 1
   endif
   set DLLIB = ""
   set JAVA_ARCH = ""
   breaksw

case SLINUX:
   set COMPILE_DC = 0
   set OFLAG = "-O"
   if ( $OPTIMIZATION == 1 ) then
      set OFLAG = "-O3"
   endif   

   set CC = "gcc"
   set CXX = "g++"

   if ( $DEBUGGED == 1) then
      set DEBUG_FLAG = "-g"
      set OFLAG = ""
   endif
   if ( $GPROFFED == 1) then
      set DEBUG_FLAG = "$DEBUG_FLAG -pg"
   endif

   set CFLAGS   = "$OFLAG -Wall -Werror -Wstrict-prototypes -DLINUX -DSLINUX -DGETHOSTBYNAME_R6 -DGETHOSTBYADDR_R8 $DEBUG_FLAG $CFLAGS"
   set DEPEND_FLAGS = "$CFLAGS $XMTINCD"
   set LFLAGS   = "$DEBUG_FLAG $LFLAGS"

   set SHAREDLD = $CC
   set SHARED_CFLAGS = "-fPIC"
   set SHARED_LFLAGS = "$SHARED_LFLAGS -L. -shared"

   set XMTDEF   = ""
   set XINCD    = "$XMTINCD $XINCD -I/usr/X11R6/include"
   set XCFLAGS  = "-DNeedFunctionPrototypes -Wno-error $XMTDEF $XINCD"
   set XLIBD    = "-L/usr/X11R6/lib"
   set XLFLAGS  = "$XLIBD"
   set XLIBS    = "-Xlinker -Bstatic -lXm -Xlinker -Bdynamic -lXpm -lXt -lXext -lX11 -lSM -lICE -lXp"
   set NOERR_CFLAG = "-Wno-error"
   set JAVA_ARCH = "linux"
   breaksw

case SOLARIS:
case SOLARIS64:
case SOLARIS86:
   set COMPILE_DC = 1
   if ($OPTIMIZATION == 1) then 
      if ( $CC == gcc ) then
         set OFLAG = "-O3"
      else
         if ( $BUILDARCH == SOLARIS86 ) then
            # Do not compile Grid Engine with -xO4 on SOLARIS86 platform
            # unless WS8 compiler with patch 112756-01 or higher is installed

            set compversion = `cc -V |& head -1 | cut -f4 -d" "`
            set patchcnt = `/bin/sh -c 'ls -d /var/sadm/patch/112756-* 2> /dev/null' | wc -l`
            set haspatch = `expr $patchcnt ">=" 1`
     #       set haspatch = 0
            if ( $compversion == 5.5 && $haspatch == 1 ) then
               set OFLAG = "-fast -xchip=pentium -xcache=generic"
            else
               echo using \"-xO3\" optimization due to compiler bug
               set OFLAG = "-xO3"
     #          set OFLAG = "-xO3 -fns -fsimple=2 -fsingle -ftrap=%none -nofstore -xbuiltin=%all -xdepend -xlibmil"
            endif
         else
            set OFLAG = "-fast -xchip=generic -xcache=generic"
         endif
      endif
   else
       set OFLAG = "-O"
   endif

   if ( $DEBUGGED == 1 ) then
      set DEBUG_FLAG = "-g $INSURE_FLAG"
      set OFLAG = ""
   endif

   if ( $GPROFFED == 1 ) then
      set OFLAG = "-O"
      if ( $CC == gcc ) then
         set DEBUG_FLAG = "-g -pg"
         set LIBS = "$LIBS -ldl"
      else
         set DEBUG_FLAG = "-g -xpg"
      endif
   endif

   set rel = `uname -r | cut -f 2 -d .`
   if ( $rel > 6 ) then
      set CFLAGS = "-DHAS_SOCKLEN_T $CFLAGS"
   endif

   set DEPEND_FLAGS = "$CFLAGS -D$BUILDARCH $XMTINCD"

   set MEMFLAG = ""
   if ( $BUILDARCH == SOLARIS64 ) then
      if ( $CC == cc ) then
         set MEMFLAG = "-xarch=v9"
         set LFLAGS = "-xarch=v9 -xildoff $LFLAGS"
      endif
   else
      set CFLAGS = "-D_LARGEFILE64_SOURCE=1 $CFLAGS"
      set MEMFLAG = "-xarch=generic"
      if ( $CC == cc ) then
         set MEMCFLAG = "-Xa"
         set LFLAGS = "-xildoff $LFLAGS"
      endif
   endif

   if ( $CC == cc ) then
      set CFLAGS = "-Xc -v $OFLAG $MEMFLAG -ftrap=division $CFLAGS -DSOLARIS -D$BUILDARCH -D__EXTENSIONS__ -D_POSIX_C_SOURCE=199506L -DGETHOSTBYNAME_R5 -DGETHOSTBYADDR_R7 $DEBUG_FLAG"
   endif

   set SHAREDLD = $CC
   if ( $CC == gcc ) then
      set SHARED_CFLAGS = "-fPIC"
      set SHARED_LFLAGS = "$SHARED_LFLAGS -L. -shared"
   else
      set SHARED_CFLAGS = "-KPIC"
      if ( $BUILDARCH == SOLARIS64 ) then
         set SHARED_LFLAGS = "$SHARED_LFLAGS -L. -G -xarch=v9"
      else
         set SHARED_LFLAGS = "$SHARED_LFLAGS -L. -G"
      endif
   endif

   set LFLAGS   = "$LFLAGS $DEBUG_FLAG"
   set LIBS     = "-lsocket -lnsl $LIBS"
   set LOADAVGLIBS = "-lkvm -lkstat"

   set XMTDEF   = "-DXMT_CDE_BUG_FIX"

   set XINCD    = "-I/usr/dt/include -I/usr/openwin/include $XMTINCD $XINCD"
   set XLIBD    = "-L/usr/dt/lib -L/usr/openwin/lib"
   set XCFLAGS  = "-Xc -DNeedFunctionPrototypes -DNeedVarargsPrototypes $XMTDEF $XINCD"
   set XLFLAGS  = "$XLIBD"
   set XLIBS    = "-lXm -lXt -lXext -lXmu -lX11 -lw -lgen"

   set RANLIB   = "echo"
   set JAVA_ARCH = "solaris"
   breaksw

case UNICOS*:
   set INTERACTTARGETS = ""
   set MAKE = gmake
   set ARFLAGS = rcv
   switch ($BUILDARCH)
      case UNICOS_TS:
         set CFLAGS   = "-DCRAY -DCRAYTS -DGETHOSTBYNAME_M $DEBUG_FLAG $CFLAGS"
         breaksw
      case UNICOS_TS_IEEE:
         set CFLAGS   = "-DCRAY -DCRAYTS -DCRAYTSIEEE -DGETHOSTBYNAME_M -DGETHOSTBYADDR_M $DEBUG_FLAG $CFLAGS"
         breaksw
      default:
         set CFLAGS   = "-DCRAY -DGETHOSTBYNAME_M $DEBUG_FLAG $CFLAGS"
   endsw
   set DEPEND_FLAGS = "$CFLAGS $XMTINCD"
   set LFLAGS   = ""
   set XMTDEF   = ""
   switch (`/bin/uname -r`)
      case "[0-8].*":
      case "9.[01]*":
         set XINCD    = "-I/usr/src/net/cvt/motif/lib $XMTINCD $XINCD"
         set XLIBD    = "-L/usr/src/net/cvt/motif/lib/Xm"
         set XLIBS    = "-lXm -lXt -lXext -lX11"
         breaksw
      default:
         set XINCD    = "$XMTINCD $XINCD"
         set XLIBD    = ""
         set XLIBS    = "-lXm -lXt -lXext -lX11 -lSM -lICE"
         breaksw
   endsw
   set XCFLAGS  = "$XMTDEF $XINCD"
   set XLFLAGS  = "$XLIBD"
   set COMPILE_DC = 1
   if ( $SHAREDLIBS == 1 ) then
      echo creating shared libs not yet supported on $BUILDARCH
      exit 1
   endif
   set JAVA_ARCH = ""
   breaksw
default:
   echo "ERROR: Architecture $BUILDARCH not supported by aimk"
   exit 1
endsw

if ($JAVA == 1 && "$JAVA_ARCH" != "") then
   set CFLAGS = "$CFLAGS -I${JAVA_HOME}/include/${JAVA_ARCH}"
endif

set MAKEOPT = "$MAKEOPT $PARALLEL"

# set gcc version dependent defines, suppress gcc 3.3.x warnings
#
if ($CC == gcc) then
   expr `gcc -dumpversion` : 3.3 > /dev/null
   if ($status == 0) then
      set CFLAGS = "$CFLAGS -Wno-sign-compare -Wno-strict-aliasing"
   endif
   expr `gcc -dumpversion` : 2.96 > /dev/null
   if ($status == 0) then
      set CFLAGS = "-DUSING_GCC_2_96 $CFLAGS"
   endif
endif

# pass -DCOMPILE_DC define to compile where PTF and PDC are ported
#
if ( $COMPILE_DC == 1 ) then
   set CFLAGS = "$CFLAGS -DCOMPILE_DC"
endif

# create only shared libraries if requested - this is not yet the default
#
if ( $SHAREDLIBS == 1 ) then
   set LIBEXT = "$SHAREDEXT"
else
   set LIBEXT = ".a"
endif

# settings for spooling libraries - shared vs. static
# if we use the dynamic spooling, we need shared spooling libs
if ( $SPOOLING_METHOD == "dynamic" ) then
   set SPOOLLIBEXT = "$SHAREDEXT"
else
   set SPOOLLIBEXT = $LIBEXT
endif

# for gettext
#
if ( $WITH_GETTEXT == 1 ) then
   set   XGETTEXT    = "$GETTEXTBASE/$BUILDARCH/bin/xgettext --strict --foreign-user -k -k'sge_gettext' --language=C -j -o $MESSAGES"
   if ( "$?MYILIB" ) then
      set LFLAGS = "-L $MYILIB"
      echo "Using $MYILIB path for i18n support"
   else
      if ( $BUILDARCH == "SOLARIS" || $BUILDARCH == "SOLARIS64" || $BUILDARCH == "SOLARIS86" || $BUILDARCH == "LINUX86" || $BUILDARCH == LINUXIA64 || $BUILDARCH == LINUXAMD64) then
         set LIBINTL = ""
         set HFLAGS = ""
      else
         set HFLAGS = "-I$GETTEXTBASE/$BUILDARCH/include"
         set LFLAGS = "-L$GETTEXTBASE/$BUILDARCH/lib $LFLAGS"
         set LIBINTL = "-lintl -lnlsut"
      endif
   endif
   set LIBS = "$LIBINTL $LIBS"
   set CFLAGS = "$CFLAGS -D__SGE_COMPILE_WITH_GETTEXT__ $HFLAGS"
else
   set XGETTEXT = ""
endif

# for usermapping
#
if ( $WITH_USERMAPPING == 0 ) then
   set CFLAGS = "$CFLAGS -D__SGE_NO_USERMAPPING__"
endif

# create shared lib from LGPL code  - use $SHAREDEXT for shared lib extension
#
set GSHARED_CFLAGS = "$SHARED_CFLAGS"
set GSHARED_LFLAGS = "$SHARED_LFLAGS"

#---------------------------------------------------------------------------
# Actual make targets begin here
#
if ("$COMPILE_DIR" == "") then
   echo making in $COMPILE_ARCH/ for $BUILDARCH
else
   echo making in $COMPILE_DIR/ for $BUILDARCH
endif

if ( "$BUILDMAN" == 1 ) then
   echo _________M_A_N_P_A_G_E_S__________________

   if ( $?MAN2HTMLPATH ) then
      setenv PATH ${PATH}:${MAN2HTMLPATH}
   endif   
   if ( $?GROFFPATH ) then
      setenv PATH ${PATH}:${GROFFPATH}
   else
      echo "groff must exist"
      echo not done
      exit 1
   endif   

   $SOURCE/scripts/mk_ascman $SOURCE $SGE_PRODUCT_MODE $BUILDMAN $BUILDHTMLMAN $KVMAN

   if ( $status != 0 ) then
      echo not done
      exit 1
   else
      echo done
      exit 0
   endif
endif

if ( "$BUILDCATMAN" == 1 ) then
   echo _________C_A_T_P_A_G_E_S__________________

   $SOURCE/scripts/mk_catman $SOURCE $SGE_PRODUCT_MODE

   if ( $status != 0 ) then
      echo not done
      exit 1
   else
      echo done
      exit 0
   endif

endif

if ( "$BUILDGETTEXT" == 1 ) then
   echo _________G_E_T_T_E_X_T____________________

   cd $SOURCE/3rdparty/gettext
   $SOURCE/scripts/install.gridware $COMPILE_ARCH
   if ( $status != 0 ) then
      echo not done
      exit 1
   else
      cd $SOURCE
      echo done
      exit 0
   endif

endif   

if ( "$BUILDJAVADOC" == 1 ) then
   echo _________J_A_V_A_D_O_C_S__________________

   cd $SOURCE/$COMPILE_ARCH

   if ( ! -d ../../doc/javadoc ) then
      echo " INFO: JavaDoc directory does not exist. Creating"
      mkdir ../../doc/javadoc
   else
      echo " INFO: clearing old JavaDoc directory."
      rm -Rf ../../doc/javadoc/*
   endif

   $JAVA_HOME/bin/javadoc -sourcepath $SOURCE/classes -classpath . -d ../../doc/javadoc -version org.ggf.drmaa

   if ( $status != 0 ) then
      echo not done
      exit 1
   endif

   cd $SOURCE
endif

if ( "$SGE_DEPEND" == 1 ) then
   echo _________S_G_E__D_E_P_E_N_D_______________

   cd $SOURCE/3rdparty/sge_depend

   if ( ! -d $COMPILE_ARCH ) then
      echo " INFO: Architecture dir $COMPILE_ARCH not existing for sge_depend. Making"
      mkdir $COMPILE_ARCH
   endif

   cd $COMPILE_ARCH
   $MAKE $MAKEOPT -f ../Makefile "CC=$CC" "CFLAGS=$CFLAGS $XCFLAGS -U_GNU_SOURCE" "LFLAGS=$LFLAGS" "COMPILE_ARCH=$COMPILE_ARCH" $*

   if ( $status != 0 ) then
      echo not done
      exit 1
   endif

   cd $SOURCE
endif

if ( "$MAKEWINGRIDLIB" == 1 ) then
   echo _________L_I_B__W_I_N_G_R_I_D_____________

   cd $COMPILE_ARCH

   switch ( "$argv" )
      case clean:
         set param = clean
         set DEPEND = ""
      breaksw

      case depend:
         set param = depend
         set DEPEND = "../3rdparty/sge_depend/$COMPILE_ARCH/sge_depend -l -S -Y $DEPEND_FLAGS"
      breaksw

      case messages:
         set param = messages
         set DEPEND = ""
      breaksw

      default:
         set param = wingridlib
         set DEPEND = ""
         set LIBS = "$LIBS -lwingrid" 
      breaksw
   endsw

   
  $MAKE $MAKEOPT -f $SOURCE/libs/wingrid/Makefile "MAKE=$MAKE" "CC=$CC" \
      "COMPILE_ARCH=$COMPILE_ARCH" \
      "CFLAGS=$CFLAGS $CORE_INCLUDE" "SOURCE=$SOURCE" \
      "KRBLIBS=$KRBLIBS" \
      "LFLAGS=$LFLAGS $KLFLAGS" "STRIP=$STRIP" "RANLIB=$RANLIB" \
      "AR=$AR" "ARFLAGS=$ARFLAGS" \
      "SHARED_CFLAGS=$SHARED_CFLAGS" "SHARED_LFLAGS=$SHARED_LFLAGS" \
      "DEPEND_FLAGS=$DEPEND_FLAGS" \
      "SHAREDLD=$SHAREDLD" "SHAREDEXT=$SHAREDEXT" "LIBEXT=$LIBEXT" \
      "SDIR=$SDIR" "DEPEND=$DEPEND" $param

   if ( $status != 0 ) then
      echo not done
      exit 1
   endif

   cd $SOURCE
endif

if ( "$MAKESTRPTIME" == 1 ) then
   echo _________3_r_d_p_a_r_t_y__S_T_R_P_T_I_M_E_

   # Set TPLIB, LIBS, and CFLAGS for all compiles

   set TPLIB = "$TPLIB libstrptime.a"

   set LIBS = "$LIBS -lstrptime"

   set CFLAGS = "-I$SOURCE/3rdparty/strptime $CFLAGS"

   cd $COMPILE_ARCH

  $MAKE $MAKEOPT -f $SOURCE/3rdparty/strptime/Makefile "MAKE=$MAKE" "CC=$CC" \
      "COMPILE_ARCH=$COMPILE_ARCH" \
      "CFLAGS=$CFLAGS" "SOURCE=$SOURCE" \
      "KRBLIBS=$KRBLIBS" \
      "LFLAGS=$LFLAGS $KLFLAGS" "STRIP=$STRIP" "RANLIB=$RANLIB" \
      "AR=$AR" "ARFLAGS=$ARFLAGS" \
      "SHARED_CFLAGS=$SHARED_CFLAGS" "SHARED_LFLAGS=$SHARED_LFLAGS" \
      "DEPEND_FLAGS=$DEPEND_FLAGS" \
      "SHAREDLD=$SHAREDLD" "SHAREDEXT=$SHAREDEXT" "LIBEXT=$LIBEXT" $argv

   if ( $status != 0 ) then
      echo not done
      exit 1
   endif

   cd $SOURCE
endif

if ( "$MAKEFNMATCH" == 1 ) then
   echo _________3_r_d_p_a_r_t_y__F_N_M_A_T_C_H___

   # Set TPLIB, LIBS, and CFLAGS for all compiles

   set TPLIB = "$TPLIB libfnmatch.a"

   set LIBS = "$LIBS -lfnmatch"

   set CFLAGS = "-I$SOURCE/3rdparty/fnmatch $CFLAGS"

   cd $COMPILE_ARCH

  $MAKE $MAKEOPT -f $SOURCE/3rdparty/fnmatch/Makefile "MAKE=$MAKE" "CC=$CC" \
      "COMPILE_ARCH=$COMPILE_ARCH" \
      "CFLAGS=$CFLAGS" "SOURCE=$SOURCE" \
      "KRBLIBS=$KRBLIBS" \
      "LFLAGS=$LFLAGS $KLFLAGS" "STRIP=$STRIP" "RANLIB=$RANLIB" \
      "AR=$AR" "ARFLAGS=$ARFLAGS" \
      "SHARED_CFLAGS=$SHARED_CFLAGS" "SHARED_LFLAGS=$SHARED_LFLAGS" \
      "DEPEND_FLAGS=$DEPEND_FLAGS" \
      "SHAREDLD=$SHAREDLD" "SHAREDEXT=$SHAREDEXT" "LIBEXT=$LIBEXT" $argv

   if ( $status != 0 ) then
      echo not done
      exit 1
   endif

   cd $SOURCE
endif

if ( "$ADD_COPYRIGHT" == 1 ) then
   set CFLAGS = "-DADD_COPYRIGHT -I$COPYRIGHT_DIR $CFLAGS"
else if ( "$ADD_SUN_COPYRIGHT" == 1 ) then
   set CFLAGS = "-DADD_COPYRIGHT -DADD_SUN_COPYRIGHT -I$SUN_COPYRIGHT_DIR $CFLAGS"
endif

if ( "$CORE" == 1 ) then
   if ( "$COMPILE_DIR" == "" ) then
      cd $COMPILE_ARCH
   else
      cd $COMPILE_DIR
   endif

   # spooledit is only available if berkeleydb is available.
   # if we build with spooling method classic, touch spooledit
   if ( $SPOOLING_METHOD == classic ) then
      touch -t 202012312358 libspoolb.a spooledit.o
      touch -t 202012312359 spooledit
   endif

   # we must map the symbolic spooling lib names the target filenames,
   # otherwise make will always relink qmaster, spoolinit ...
   set new_deps = ""
   foreach dep ( $SPOOLING_DEPS )
      switch ( $dep )
         case spoolloaderlib:
            set new_deps = "$new_deps libspoolloader$LIBEXT"
            breaksw
         case spooldlib:
            set new_deps = "$new_deps libspoold$LIBEXT"
            breaksw
         case spoolblib:
            set new_deps = "$new_deps libspoolb$SPOOLLIBEXT"
            breaksw
         case spoolclib:
            set new_deps = "$new_deps libspoolc$SPOOLLIBEXT"
            breaksw
         case spoollib:
            set new_deps = "$new_deps libspool$LIBEXT"
            breaksw
      endsw
   end
   set SPOOLING_DEPS = "$new_deps"

   echo _________C_O_R_E__S_Y_S_T_E_M_____________
   $MAKE $MAKEOPT -f $MAKEFILE "MAKE=$MAKE" "CC=$CC" \
         "LD_WRAPPER=$LD_WRAPPER" \
         "CFLAGS=$CFLAGS $CORE_INCLUDE" \
         "DEPEND_FLAGS=$DEPEND_FLAGS $CORE_INCLUDE" \
         "SDIR=$SDIR" "SOURCE=$SOURCE" "COMPILE_ARCH=$COMPILE_ARCH" \
         "SECTARGETS=$SECTARGETS" "SECDEPEND=$SECDEPEND" "SECCLEAN=$SECCLEAN" \
         "SECLIBOBJS=$SECLIBOBJS" "SECLIB=$SECLIB" "SECLIBS=$SECLIBS"\
         "KRBTARGETS=$KRBTARGETS" "KRBDEPEND=$KRBDEPEND" "KRBCLEAN=$KRBCLEAN"\
         "KRBLIBS=$KRBLIBS" "KRBLIB=$KRBLIB" "GSSDEPEND=$GSSDEPEND" \
         "GSSCLEAN=$GSSCLEAN" "MSDIR=$MSDIR" "SLIBS=$GSSLIBS $KLIBS" "LIBS=$LIBS" \
         "XCFLAGS=$XCFLAGS" "XLFLAGS=$XLFLAGS" \
         "XLIBS=$XLIBS" "LFLAGS=$LFLAGS $KLFLAGS" "STRIP=$STRIP" \
         "RANLIB=$RANLIB" "AR=$AR" "ARFLAGS=$ARFLAGS" \
         "NOERR_PROTO=$NOERR_PROTO" \
         "NOERR_CFLAG=$NOERR_CFLAG" \
         "COMPILE_DC=$COMPILE_DC" "LOADAVGLIBS=$LOADAVGLIBS" "TPLIB=$TPLIB" \
         "XGETTEXT=$XGETTEXT" "MESSAGES=$MESSAGES" \
         "INTERACTTARGETS=$INTERACTTARGETS" \
         "LIBEXT=$LIBEXT" "SHAREDEXT=$SHAREDEXT" "SHAREDLD=$SHAREDLD" \
         "SHARED_CFLAGS=$SHARED_CFLAGS" "SHARED_LFLAGS=$SHARED_LFLAGS $KLFLAGS" \
         "SPOOLING_LIBS=$SPOOLING_LIBS" "SPOOLING_DEPS=$SPOOLING_DEPS" \
         "SPOOLLIBEXT=$SPOOLLIBEXT" \
         "BDB_LIB_SUFFIX=${BDB_LIB_SUFFIX}" \
         "DLLIB=$DLLIB" \
         "MEMCFLAG=$MEMCFLAG" \
         "CORE_INCLUDE=$CORE_INCLUDE" \
         "PERLGUI=$PERLGUI" "PERL=$PERL" "SWIG=$SWIG" \
         $argv
   if ( $status != 0 ) then
      echo not done
      exit 1
   endif

   if ( $JAVA == 1 ) then
      $MAKE $MAKEOPT -f $MAKEFILE "MAKE=$MAKE" "CC=$CC" \
            "LD_WRAPPER=$LD_WRAPPER" \
            "CFLAGS=$CFLAGS $CORE_INCLUDE" \
            "DEPEND_FLAGS=$DEPEND_FLAGS $CORE_INCLUDE" \
            "SDIR=$SDIR" "SOURCE=$SOURCE" "COMPILE_ARCH=$COMPILE_ARCH" \
            "SECTARGETS=$SECTARGETS" "SECDEPEND=$SECDEPEND" "SECCLEAN=$SECCLEAN" \
            "SECLIBOBJS=$SECLIBOBJS" "SECLIB=$SECLIB" "SECLIBS=$SECLIBS"\
            "KRBTARGETS=$KRBTARGETS" "KRBDEPEND=$KRBDEPEND" "KRBCLEAN=$KRBCLEAN"\
            "KRBLIBS=$KRBLIBS" "KRBLIB=$KRBLIB" "GSSDEPEND=$GSSDEPEND" \
            "GSSCLEAN=$GSSCLEAN" "MSDIR=$MSDIR" "SLIBS=$GSSLIBS $KLIBS" "LIBS=$LIBS" \
            "XCFLAGS=$XCFLAGS" "XLFLAGS=$XLFLAGS" \
            "XLIBS=$XLIBS" "LFLAGS=$LFLAGS $KLFLAGS" "STRIP=$STRIP" \
            "RANLIB=$RANLIB" "AR=$AR" "ARFLAGS=$ARFLAGS" \
            "NOERR_PROTO=$NOERR_PROTO" \
            "NOERR_CFLAG=$NOERR_CFLAG" \
            "COMPILE_DC=$COMPILE_DC" "LOADAVGLIBS=$LOADAVGLIBS" "TPLIB=$TPLIB" \
            "XGETTEXT=$XGETTEXT" "MESSAGES=$MESSAGES" \
            "INTERACTTARGETS=$INTERACTTARGETS" \
            "LIBEXT=$LIBEXT" "SHAREDEXT=$SHAREDEXT" "SHAREDLD=$SHAREDLD" \
            "SHARED_CFLAGS=$SHARED_CFLAGS" "SHARED_LFLAGS=$SHARED_LFLAGS $KLFLAGS" \
            "SPOOLING_LIBS=$SPOOLING_LIBS" "SPOOLING_DEPS=$SPOOLING_DEPS" \
            "SPOOLLIBEXT=$SPOOLLIBEXT" \
            "DLLIB=$DLLIB" \
            "MEMCFLAG=$MEMCFLAG" \
            "CORE_INCLUDE=$CORE_INCLUDE" \
            classes_all
      if ( $status != 0 ) then
         echo not done
         exit 1
      endif
   endif

   cd $SOURCE
endif

if ( "$QMON" == 1 ) then
   echo _________Q_M_O_N__________________________

   cd $SOURCE/3rdparty/qmon

   if (! -d $COMPILE_ARCH) then
      echo " INFO: Architecture dir $COMPILE_ARCH not existing for 3rdparty Qmon Libs. Making"
      mkdir $COMPILE_ARCH
   endif

   cd $COMPILE_ARCH

   $MAKE $MAKEOPT -f ../Makefile "MAKE=$MAKE" "CC=$CC" \
         "LD_WRAPPER=$LD_WRAPPER" \
         "CFLAGS=$CFLAGS" "SDIR=$SDIR" "SOURCE=$SOURCE" \
         "COMPILE_ARCH=$COMPILE_ARCH" \
         "XCFLAGS=$XCFLAGS" "XLFLAGS=$XLFLAGS" "KRBLIBS=$KRBLIBS" \
         "XLIBS=$XLIBS" "LFLAGS=$LFLAGS $KLFLAGS" "STRIP=$STRIP" "RANLIB=$RANLIB" \
         "AR=$AR" "ARFLAGS=$ARFLAGS" "TPLIB=$TPLIB" \
         "MEMCFLAG=$MEMCFLAG"  \
         "SHARED_CFLAGS=$SHARED_CFLAGS" "SHARED_LFLAGS=$SHARED_LFLAGS" \
         "GSHARED_CFLAGS=$GSHARED_CFLAGS" "GSHARED_LFLAGS=$GSHARED_LFLAGS" \
         "SHAREDLD=$SHAREDLD" "SHAREDEXT=$SHAREDEXT" "LIBEXT=$LIBEXT" $argv

   if ( $status != 0 ) then
      echo not done
      exit 1
   endif

   cd $SOURCE

   cd $COMPILE_ARCH

   $MAKE $MAKEOPT -f $SOURCE/clients/qmon/Makefile \
         "MAKE=$MAKE" "CC=$CC" \
         "LD_WRAPPER=$LD_WRAPPER" \
         "CFLAGS=$CFLAGS $CORE_INCLUDE" \
         "DEPEND_FLAGS=$DEPEND_FLAGS $CORE_INCLUDE" \
         "SDIR=$SDIR" "SOURCE=$SOURCE" "COMPILE_ARCH=$COMPILE_ARCH" \
         "SECTARGETS=$SECTARGETS" "SECDEPEND=$SECDEPEND" "SECCLEAN=$SECCLEAN" \
         "SECLIBOBJS=$SECLIBOBJS" "SECLIB=$SECLIB" "SECLIBS=$SECLIBS"\
         "KRBTARGETS=$KRBTARGETS" "KRBDEPEND=$KRBDEPEND" "KRBCLEAN=$KRBCLEAN"\
         "KRBLIBS=$KRBLIBS" "KRBLIB=$KRBLIB" "GSSDEPEND=$GSSDEPEND" \
         "GSSCLEAN=$GSSCLEAN" "MSDIR=$MSDIR" "SLIBS=$GSSLIBS $KLIBS" "LIBS=$LIBS" \
         "XCFLAGS=$XCFLAGS" "XLFLAGS=$XLFLAGS" \
         "XLIBS=$XLIBS" "LFLAGS=$LFLAGS $KLFLAGS" "STRIP=$STRIP" \
         "RANLIB=$RANLIB" "AR=$AR" "ARFLAGS=$ARFLAGS" \
         "NOERR_PROTO=$NOERR_PROTO" \
         "NOERR_CFLAG=$NOERR_CFLAG" \
         "COMPILE_DC=$COMPILE_DC" "LOADAVGLIBS=$LOADAVGLIBS" "TPLIB=$TPLIB" \
         "XGETTEXT=$XGETTEXT" "MESSAGES=$MESSAGES" \
         "INTERACTTARGETS=$INTERACTTARGETS" \
         "LIBEXT=$LIBEXT" "SHAREDEXT=$SHAREDEXT" "SHAREDLD=$SHAREDLD" \
         "SHARED_CFLAGS=$SHARED_CFLAGS" "SHARED_LFLAGS=$SHARED_LFLAGS $KLFLAGS" \
         "SPOOLLIBEXT=$SPOOLLIBEXT" \
         "SPOOLING_LIBS=$SPOOLING_LIBS" "SPOOLING_DEPS=$SPOOLING_DEPS" \
         "DLLIB=$DLLIB" \
         "MEMCFLAG=$MEMCFLAG" \
         $argv
   if ( $status != 0 ) then
      echo not done
      exit 1
   endif
   
   cd $SOURCE
endif

if ( "$REMOTE" == 1) then
   echo _________3_r_d_p_a_r_t_y__R_E_M_O_T_E_____

   cd $SOURCE/3rdparty/remote

   if ( ! -d $COMPILE_ARCH ) then
      echo " INFO: Architecture dir $COMPILE_ARCH not existing for 3rdparty remote. Making"
      mkdir $COMPILE_ARCH
   endif

   if ( $COMPILE_ARCH == "NECSX5" ) then
      set CFLAGS_REMOTE = "$CFLAGS_NOOPT"
   else
      set CFLAGS_REMOTE = "$CFLAGS"
   endif

   cd $COMPILE_ARCH

   $MAKE $MAKEOPT -f ../Makefile "MAKE=$MAKE" "CC=$CC" \
      "CFLAGS=$CFLAGS_REMOTE" "LIBS=$LIBS" "SOURCE=$SOURCE" \
      "KRBLIBS=$KRBLIBS" "DLLIB=$DLLIB" \
      "LFLAGS=$LFLAGS $KLFLAGS" "STRIP=$STRIP" "RANLIB=$RANLIB" \
      "AR=$AR" "ARFLAGS=$ARFLAGS" "LIBEXT=$LIBEXT" \
      "MEMCFLAG=$MEMCFLAG" "COMPILE_ARCH=$COMPILE_ARCH" $argv

   if ( $status != 0 ) then
      echo not done
      exit 1
   endif

   cd $SOURCE
endif

if ( "$QMAKE" == 1) then
   echo _________3_r_d_p_a_r_t_y__Q_M_A_K_E_______

   if ( ! -d $SOURCE/3rdparty/qmake/$COMPILE_ARCH || \
	! -r $SOURCE/3rdparty/qmake/$COMPILE_ARCH/Makefile ) then
      mkdir -p $SOURCE/3rdparty/qmake/$COMPILE_ARCH
      cd $SOURCE/3rdparty/qmake/$COMPILE_ARCH
      env REMOTE=sge CFLAGS="${CFLAGS}" SGE_ARCH=$buildarch \
       ../configure --with-grd=${COMPILE_ARCH} --srcdir=..
   endif

   cd $SOURCE/3rdparty/qmake/$COMPILE_ARCH

   $MAKE $MAKEOPT -f Makefile "MAKE=$MAKE" "COMPILE_ARCH=$COMPILE_ARCH" "DLLIB=$DLLIB" $argv

   if ( $status != 0 ) then
      echo not done
      exit 1
   endif

   cd $SOURCE
endif


if ( "$QTCSH" == 1) then
   echo _________3_r_d_p_a_r_t_y__Q_T_C_S_H_______

   # with shared libs: qtcsh requires finding libsge.so during build
   # for AIX, we have to pass special options to make
   set SGE_COPTS = ""
   set SGE_LOPTS = ""


   if ( $SHAREDLIBS == 1 ) then
      set SHARED_PATH_NAME = `dist/util/arch -lib`

      switch ( $COMPILE_ARCH )
         case AIX43:
            set SGE_LOPTS = "-brtl"
            breaksw
      endsw

      setenv $SHARED_PATH_NAME ${SOURCE}/${COMPILE_ARCH}
   endif

   set SGE_LIBS = "-lsge -lpthread"

   if ( "$COMPILE_ARCH" == "NECSX5" ) then
      set SGE_LIBS = "$SGE_LIBS -lfnmatch"
   endif

   if ( ! -d $SOURCE/3rdparty/qtcsh/$COMPILE_ARCH || \
	! -r $SOURCE/3rdparty/qtcsh/$COMPILE_ARCH/Makefile ) then
      mkdir -p $SOURCE/3rdparty/qtcsh/$COMPILE_ARCH
      cd $SOURCE/3rdparty/qtcsh/$COMPILE_ARCH
      env REMOTE=sge CFLAGS="${CFLAGS}" SGE_ARCH=$buildarch \
       ../configure --with-grd=${COMPILE_ARCH} --srcdir=..
   endif

   cd $SOURCE/3rdparty/qtcsh/$COMPILE_ARCH

   if ( $CC == "insure" ) then
      setenv CC insure
   endif

   $MAKE $MAKEOPT -f Makefile "MAKE=$MAKE" "COMPILE_ARCH=$COMPILE_ARCH" \
         "SGE_LIBS=$SGE_LIBS $KRBLIB $LFLAGS $KLFLAGS $KLIBS $DLLIB" \
         "SGE_COPTS=$SGE_COPTS" "SGE_LOPTS=$SGE_LOPTS" "DLLIB=$DLLIB" $argv

   if ( $status != 0 ) then
      echo not done
      exit 1
   endif

   if ( $SHAREDLIBS == 1 ) then
      unsetenv $SHARED_PATH_NAME
   endif

   cd $SOURCE
endif

if ( "$ADOC" == 1) then
   echo _________3_r_d_p_a_r_t_y__A_D_O_C_________

   cd $SOURCE/3rdparty/adoc

   if ( ! -d $COMPILE_ARCH ) then
      echo " INFO: Architecture dir $COMPILE_ARCH not existing for 3rdparty adoc. Making"
      mkdir $COMPILE_ARCH
   endif

   cd $COMPILE_ARCH

   $MAKE $MAKEOPT -f ../Makefile.aimk "MAKE=$MAKE" "CC=$CC" "CFLAGS=$CFLAGS" \
         "LFLAGS=$LFLAGS" $argv

   if ( $status != 0 ) then
      echo not done
      exit 1
   endif

   cd $SOURCE

   unset noglob

   set DIR_LIBS = "{libs/*}"
   set DIR_CORE = "{common,clients/*,daemons/*}"
   set DIR_TESTSUITE = "{../testsuite,../testsuite/*,../testsuite/*/*,../testsuite/*/*/*,../testsuite/*/*/*/*}"
   set SOURCE_LIBS = "$DIR_LIBS/*.c $DIR_LIBS/*.h"
   set SOURCE_CORE = "$DIR_CORE/*.c $DIR_CORE/*.h"
   set SOURCE_TESTSUITE = "$DIR_TESTSUITE/*.tcl $DIR_TESTSUITE/*.exp"

   set EDITION = "1.0"
   set REVISION = `grep GDI_VERSION libs/gdi/version.* | cut -f 2 -d '"'`

   echo "Creating documentation for libs: libcomm.texi"
   3rdparty/adoc/$COMPILE_ARCH/adoc --no-warnings -f -b -DPROJECT "Grid Engine Communication Library" -DEDITION "$EDITION" -DREVISION "SGE $REVISION" -DCOPYRIGHT "The Grid Engine Team" -DAUTHOR "The Grid Engine Team" -I -xon -xitemize -2 libs/comm/*.h libs/comm/*.c -o $COMPILE_ARCH/libcomm.texi

   echo "Creating documentation for libs: libcull.texi"
   3rdparty/adoc/$COMPILE_ARCH/adoc --no-warnings -f -b -DPROJECT "Grid Engine CULL Library" -DEDITION "$EDITION" -DREVISION "SGE $REVISION" -DCOPYRIGHT "The Grid Engine Team" -DAUTHOR "The Grid Engine Team" -I -xon -xitemize -2 libs/cull/*.h libs/cull/*.c -o $COMPILE_ARCH/libcull.texi

   echo "Creating documentation for libs: eventclient.texi"
   3rdparty/adoc/$COMPILE_ARCH/adoc --no-warnings -f -b -DPROJECT "Event Client Interface" -DEDITION "$EDITION" -DREVISION "SGE $REVISION" -DCOPYRIGHT "The Grid Engine Team" -DAUTHOR "The Grid Engine Team" -I -xon -xitemize -2 libs/evc/*.h libs/evc/*.c libs/evm/*.h libs/evm/*.c -o $COMPILE_ARCH/eventclient.texi

   echo "Creating documentation for libs: libgdi.texi"
   3rdparty/adoc/$COMPILE_ARCH/adoc --no-warnings -f -b -DPROJECT "Grid Engine GDI Library" -DEDITION "$EDITION" -DREVISION "SGE $REVISION" -DCOPYRIGHT "The Grid Engine Team" -DAUTHOR "The Grid Engine Team" -I -xon -xitemize -2 libs/gdi/*.h libs/gdi/*.c -o $COMPILE_ARCH/libgdi.texi

   echo "Creating documentation for libs: libmir.texi"
   3rdparty/adoc/$COMPILE_ARCH/adoc --no-warnings -f -b -DPROJECT "Grid Engine Mirror Interface" -DEDITION "$EDITION" -DREVISION "SGE $REVISION" -DCOPYRIGHT "The Grid Engine Team" -DAUTHOR "The Grid Engine Team" -I -xon -xitemize -2 libs/mir/*.h libs/mir/*.c -o $COMPILE_ARCH/libmir.texi

   echo "Creating documentation for libs: librmon.texi"
   3rdparty/adoc/$COMPILE_ARCH/adoc --no-warnings -f -b -DPROJECT "Grid Engine RMON Library" -DEDITION "$EDITION" -DREVISION "SGE $REVISION" -DCOPYRIGHT "The Grid Engine Team" -DAUTHOR "The Grid Engine Team" -I -xon -xitemize -2 libs/rmon/*.h libs/rmon/*.c -o $COMPILE_ARCH/librmon.texi

   echo "Creating documentation for libs: libsched.texi"
   3rdparty/adoc/$COMPILE_ARCH/adoc --no-warnings -f -b -DPROJECT "Grid Engine SCHED Library" -DEDITION "$EDITION" -DREVISION "SGE $REVISION" -DCOPYRIGHT "The Grid Engine Team" -DAUTHOR "The Grid Engine Team" -I -xon -xitemize -2 libs/sched/*.h libs/sched/*.c -o $COMPILE_ARCH/libsched.texi

   echo "Creating documentation for libs: libsgeobj.texi"
   3rdparty/adoc/$COMPILE_ARCH/adoc --no-warnings -f -b -DPROJECT "Grid Engine SGEOBJ Library" -DEDITION "$EDITION" -DREVISION "SGE $REVISION" -DCOPYRIGHT "The Grid Engine Team" -DAUTHOR "The Grid Engine Team" -I -xon -xitemize -2 libs/sgeobj/*.h libs/sgeobj/*.c -o $COMPILE_ARCH/libsgeobj.texi

   echo "Creating documentation for libs: libjapi.texi"
   3rdparty/adoc/$COMPILE_ARCH/adoc --no-warnings -f -b -DPROJECT "Grid Engine Job API Library" -DEDITION "$EDITION" -DREVISION "SGE $REVISION" -DCOPYRIGHT "The Grid Engine Team" -DAUTHOR "The Grid Engine Team" -I -xon -xitemize -2 libs/japi/japi.h libs/japi/japi.c -o $COMPILE_ARCH/libjapi.texi

   echo "Creating documentation for libs: libdrmaa.texi"
endif

echo done.
exit 0
